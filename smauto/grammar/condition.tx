import entity
import types

// === Conditions ===
// Condition: AdvancedCondition | PrimitiveCondition | ConditionGroup ;
Condition: ConditionGroup | PrimitiveCondition | AdvancedCondition;

ConditionGroup:
    '(' r1=Condition ')' operator=LogicalOperator '(' r2=Condition ')'
    // (cond=Condition (operator=LogicalOperator)?)*
;

PrimitiveCondition:
    BoolCondition       |
    NumericCondition    |
    StringCondition     |
    ListCondition       |
    DictCondition
;

AdvancedCondition: InRangeCondition | MathExpression;

InRangeCondition:
    attribute=AugmentedNumericAttr  'in range'
        '[' min=NUMBER ',' max=NUMBER ']'
;

MathExpression:
	'math' '(' op=MathTerm (op=PlusOrMinus op=MathTerm)* ')'
;

PlusOrMinus: '+' | '-';
MulOrDiv: '*' | '/' ;

MathTerm: op=MathFactor (op=MulOrDiv op=MathFactor)*;

MathFactor: (sign=PlusOrMinus)?  op=MathOperand;

MathOperand: op=NUMBER | op=[NumericAttribute:FQN|+pm:entities.attributes] |
	('(' op=MathExpression ')');

StdAttr:
    'std' '(' attribute=AugmentedNumericAttr ',' size=INT ')'
;

VarAttr:
    'var' '(' attribute=AugmentedNumericAttr ',' size=INT ')'
;

MeanAttr:
    'mean' '(' attribute=AugmentedNumericAttr  ',' size=INT ')'
;

MinAttr:
    'min' '(' attribute=AugmentedNumericAttr  ',' size=INT ')'
;

MaxAttr:
    'max' '(' attribute=AugmentedNumericAttr  ',' size=INT ')'
;

MultiplyAttr:
    'mul' '(' attribute*=AugmentedNumericAttr[','] ')'
;


AugmentedAttr:
    AugmentedNumericAttr |
    AugmentedStringAttr  |
    AugmentedBoolAttr    |
    AugmentedListAttr    |
    AugmentedDictAttr
;


AugmentedNumericAttr:
    StdAttr         |
    VarAttr         |
    MeanAttr        |
    MinAttr         |
    MaxAttr         |
    MultiplyAttr    |
    SimpleNumericAttr
;


SimpleNumericAttr:
    attribute=[NumericAttribute:FQN|+pm:entities.attributes]
;

NumericCondition:
    (operand1=AugmentedNumericAttr
     operator=NumericOperator
     (operand2=NUMBER | operand2=AugmentedNumericAttr))
;

AugmentedStringAttr: SimpleStringAttr;

SimpleStringAttr:
    attribute=[StringAttribute:FQN|+pm:entities.attributes]
;

StringCondition:
    (operand1=AugmentedStringAttr
     operator=StringOperator
     (operand2=STRING | operand2=AugmentedStringAttr))
;

AugmentedBoolAttr: SimpleBoolAttr;

SimpleBoolAttr:
    attribute=[BoolAttribute:FQN|+pm:entities.attributes]
;

BoolCondition:
    (operand1=AugmentedBoolAttr
     operator=BooleanOperator
     (operand2=BOOL | operand2=AugmentedBoolAttr))
;

AugmentedListAttr: SimpleListAttr;

SimpleListAttr:
    attribute=[ListAttribute:FQN|+pm:entities.attributes]
;

ListCondition:
    (operand1=AugmentedListAttr
     operator=ListOperator
     (operand2=List | operand2=AugmentedListAttr))
;

AugmentedDictAttr: SimpleDictAttr;

SimpleDictAttr:
    attribute=[DictAttribute:FQN|+pm:entities.attributes]
;

DictCondition:
    (operand1=AugmentedDictAttr
     operator=DictOperator
     (operand2=Dict | operand2=AugmentedDictAttr))
;


// Operators

// SharedOperator: '==' | '!=' | 'is' | 'is not';

StringOperator: '~' | '!~' | '==' | '!=' | 'is not' | 'is' | 'has' | 'in' | 'not in';

NumericOperator: '>=' | '>' | '<=' | '<' | '==' | '!=' | 'is not' | 'is';

LogicalOperator: 'AND' | 'OR' | 'NOT' | 'XOR' | 'NOR' | 'XNOR' | 'NAND';

BooleanOperator: '==' | '!=' | 'is not' | 'is';

ListOperator: '==' | '!=' | 'is' | 'is not' | 'in' | 'not in';

DictOperator: '==' | '!=' | 'is' | 'is not';

// === Fully Qualified Names Rule ===
FQN: ID('.'ID)*;
