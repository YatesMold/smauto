import broker

//TODO: Model mqtt topics?
Entity:
    'Entity'
        (
        'name:' name=ID
        'type:' etype=EntityType
        'topic:' topic=STRING
        ('freq:' freq=NUMBER)?
        'broker:' broker=[Broker]
        'attributes:'
                attributes*=Attribute
        )#
    'end'
;

EntityType:
    'sensor' |
    'actuator'
;

Attribute:
    NumericAttribute | BoolAttribute | StringAttribute | ListAttribute | DictAttribute
;

NumericAttribute:
    IntAttribute | FloatAttribute
;

IntAttribute:
    '-' name=ID ':' 'int' ('->' generator=AttrValueGen ('with noise' noise=Noise)?)?
;

FloatAttribute:
    '-' name=ID ':' 'float' ('->' generator=AttrValueGen ('with noise' noise=Noise)?)?
;

StringAttribute:
    '-' name=ID ':' 'str'
;

BoolAttribute:
    '-' name=ID ':' 'bool'
;

ListAttribute:
    '-' name=ID ':' 'list'
;

DictAttribute:
    '-' name=ID ':' '{'
        items*=Attribute
        '}'
;

AttrValueGen: ConstantFun | LinearFun | GaussianFun;

Noise: UniformNoise | GaussianNoise;

GaussianFun:
    'gaussian' '(' value=NUMBER ',' maxValue=NUMBER ',' sigma=NUMBER ')'
;

ConstantFun:
    'constant' '(' value=NUMBER ')'
;

LinearFun:
    'linear' '(' start=NUMBER ',' step=NUMBER ')'
;

UniformNoise:
    'uniform' '(' min=NUMBER ',' max=NUMBER ')'
;

GaussianNoise:
    'gaussian' '(' mu=NUMBER ',' sigma=NUMBER ')'
;

