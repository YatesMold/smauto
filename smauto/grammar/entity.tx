import broker

//TODO: Model mqtt topics?
Entity:
    'Entity:'
        (
			('name:' name=ID)?
			('type:' etype=EntityType)
			('topic:' topic=STRING)
			('freq:' freq=NUMBER)?
			('broker:' broker=[Broker])
			('attributes:' '-' attributes*=Attribute['-'])
		)#
;

EntityType:
    'sensor' |
    'actuator'
;

Attribute:
    NumericAttribute | BoolAttribute | StringAttribute | ListAttribute | DictAttribute
;

NumericAttribute:
    IntAttribute | FloatAttribute
;

IntAttribute:
    name=ID ':' 'int' ('=' default=INT)? ('->' generator=AttrValueGen ('with noise' noise=Noise)?)?
;

FloatAttribute:
    name=ID ':' 'float' ('=' default=FLOAT)? ('->' generator=AttrValueGen ('with noise' noise=Noise)?)?
;

StringAttribute:
    name=ID ':' 'str' ('=' default=STRING)?
;

BoolAttribute:
    name=ID ':' 'bool' ('=' default=BOOL)?
;

ListAttribute:
    name=ID ':' 'list' ('=' '[' default*=OBJECT[','] ']')?
;

DictAttribute:
    name=ID ':' 'dict' ('=' '{' items*=Attribute[','] '}')?
;


AttrValueGen: ConstantFun | LinearFun | GaussianFun;

Noise: UniformNoise | GaussianNoise;

GaussianFun:
    'gaussian' '(' value=NUMBER ',' maxValue=NUMBER ',' sigma=NUMBER ')'
;

ConstantFun:
    'constant' '(' value=NUMBER ')'
;

LinearFun:
    'linear' '(' start=NUMBER ',' step=NUMBER ')'
;

UniformNoise:
    'uniform' '(' min=NUMBER ',' max=NUMBER ')'
;

GaussianNoise:
    'gaussian' '(' mu=NUMBER ',' sigma=NUMBER ')'
;

