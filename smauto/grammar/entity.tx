import broker

//TODO: Model mqtt topics?
Entity:
    'Entity'
        (
        'name:' name=ID
        'type:' etype=EntityType
        'topic:' topic=STRING
        'broker:' broker=[Broker]
        'attributes:'
                attributes*=Attribute
        )#
    'end'
;

EntityType:
    'sensor' |
    'actuator'
;

Attribute:
    NumericAttribute | BoolAttribute | StringAttribute | ListAttribute | DictAttribute
;

NumericAttribute:
    IntAttribute | FloatAttribute
;

IntAttribute:
    '-' name=ID ':' 'int' ('with' generator=AttrValueGen)?
;

FloatAttribute:
    '-' name=ID ':' 'float' ('with' generator=AttrValueGen)?
;

StringAttribute:
    '-' name=ID ':' 'str'
;

BoolAttribute:
    '-' name=ID ':' 'bool'
;

ListAttribute:
    '-' name=ID ':' 'list'
;

DictAttribute:
    '-' name=ID ':' '{'
        items*=Attribute
        '}'
;

AttrValueGen: UniformGen | ConstantGen;

UniformGen:
    'uniform' '(' from=NUMBER (',' to=NUMBER)? ')'
;

ConstantGen:
    'constant' '(' value=NUMBER ')'
;
