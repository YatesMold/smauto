import broker

//TODO: Model mqtt topics?
Entity:
    'Entity'
        (
        'name:' name=ID
        'type:' etype=EntityType
        'topic:' topic=STRING
        'broker:' broker=[Broker]
        'attributes:'
                attributes*=Attribute
        )#
    'end'
;

EntityType:
    'sensor' |
    'actuator'
;

Attribute:
    NumericAttribute | BoolAttribute | StringAttribute | ListAttribute | DictAttribute
;

NumericAttribute:
    IntAttribute | FloatAttribute
;

IntAttribute:
    '-' name=ID ':' 'int' ('->' generator=AttrValueGen ('with noise' noise=Noise)?)?
;

FloatAttribute:
    '-' name=ID ':' 'float' ('->' generator=AttrValueGen ('with noise' noise=Noise)?)?
;

StringAttribute:
    '-' name=ID ':' 'str'
;

BoolAttribute:
    '-' name=ID ':' 'bool'
;

ListAttribute:
    '-' name=ID ':' 'list'
;

DictAttribute:
    '-' name=ID ':' '{'
        items*=Attribute
        '}'
;

AttrValueGen: UniformFun | ConstantFun | LinearFun | GaussianFun;

Noise: UniformFun | ConstantFun | LinearFun | GaussianFun;

UniformFun:
    'uniform' '(' min=NUMBER ',' max=NUMBER ')'
;

ConstantFun:
    'constant' '(' value=NUMBER ')'
;

LinearFun:
    'linear' '(' from=NUMBER ',' to=NUMBER ',' step=NUMBER ')'
;

GaussianFun:
    'gaussian' '(' mu=NUMBER ',' sigma=NUMBER ')'
;

